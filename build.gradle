plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.dorongold.task-tree' version '2.1.0'
}

sourceSets {
    // Общие классы для тестов
    common {
        java {
            srcDir 'src/test/common/java'
        }
        resources {
            srcDir 'src/test/common/resources'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
    // Модульные тесты
    test {
        java {
            srcDir 'src/test/unit/java'
        }
        resources {
            srcDir 'src/test/unit/resources'
        }
        compileClasspath += sourceSets.common.output
        runtimeClasspath += sourceSets.common.output
    }
    // Интеграционные тесты
    integrationTest {
        java {
            srcDir 'src/test/integration/java'
        }
        resources {
            srcDir 'src/test/integration/resources'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath + sourceSets.common.output
        runtimeClasspath += output + compileClasspath
    }
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'jakarta.persistence:jakarta.persistence-api:2.2.3'
    runtimeOnly 'org.postgresql:postgresql:42.6.0'
    //
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    //
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.testcontainers:testcontainers:1.19.7'
    //integrationTestImplementation group: 'org.springframework.restdocs', name: 'spring-restdocs-mockmvc', version: '2.0.6.RELEASE'
    //
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

springBoot {
    buildInfo()
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

test {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
}

check {
    dependsOn integrationTest
}